buildscript {
    repositories.jcenter()

    dependencies {
        // docker build plugin
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.10.4"
    }
}

    
group 'de.comroid'
version '1.00'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker-compose'


// hooks 'dependsOn composeUp' and 'finalizedBy composeDown',
// and exposes environment variables and system properties (if possible) 
dockerCompose.isRequiredBy(test)

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'de.comroid.Cobalton'

wrapper {
    gradleVersion = '5.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

dockerCompose {
    useComposeFiles = ['docker-compose.yml'] // like 'docker-compose -f <file>'; default is empty
    forceRecreate = true // pass '--force-recreate' and '--renew-anon-volumes' when calling 'docker-compose up' when set to 'true`
    captureContainersOutput = true // if true, prints output of all containers to Gradle output - very useful for debugging; default is false
    environment.put 'COBALTON_VERSION', project.version.toString() // Pass environment variable to 'docker-compose' for substitution in compose file
    upAdditionalArgs = ['--build']
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
}

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    // Javacord
    implementation('org.javacord:javacord:3.0.+')
            { exclude group: 'org.fasterxml.jackson.core' }

    // Javacord Utilities
    implementation('de.comroid:javacord-utilities:1.1.0') {
        exclude group: 'org.javacord'
        exclude group: 'org.fasterxml.jackson.core'
    }

    // Socket
    implementation('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.javacord'
        exclude group: 'org.fasterxml.jackson.core'
    }
    
    // JSON Serialization Solution
    implementation('com.github.burdoto:Jimson:e96712f2ef')

    // Other dependencies
    implementation 'com.fasterxml.jackson.core:jackson-databind:3.0.0-SNAPSHOT'

    // Runtime logger dependency
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
}
